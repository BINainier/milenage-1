group 'com.github.brake.threegpp'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7

def sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: sonatypeRepositoryUrl) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                groupId = project.group
                artifactId 'milenage'
                version version
                name = project.name

                description 'Java™ library designed to support 3GPP™ Milenage algorithm calculations according to 3GPP TS 35.206'
                url 'https://github.com/brake/milenage'

//                packaging 'jar'

                inceptionYear '2016'
                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
                scm {
                    url 'https://github.com/brake/milenage'
                    connection 'scm:git:git@github.com:brake/milenage.git'
                    developerConnection 'scm:git:git@github.com:brake/milenage.git'
                }

                developers {
                    developer {
                        id 'brake'
                        name 'Constantin Roganov'
                        url 'https://github.com/brake'
                    }
                }
            }
        }
    }
}

